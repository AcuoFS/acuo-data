LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/AcuoFS/acuo-data/master/CSV/margincall/info10.csv'
AS line

MATCH (pmc {id:line.CallId})-[:DIRECTED_TO]->(c:Client)

WITH line, pmc

MERGE (pmc)-[:LAST {time:line.Time}]->(n:Step {step:line.NewStatus})

WITH pmc, n, line,
		CASE WHEN NOT ((pmc)-[:FIRST]->()) THEN [1] ELSE []
		END
	AS firstnode

FOREACH (i IN firstnode | MERGE (pmc)-[:FIRST {time:line.Time}]->(n))

WITH pmc, n, line

// We delete the previous [:LAST] relationship and add a [:NEXT] relationship

OPTIONAL MATCH (n)<-[:LAST]-(pmc)-[delRel:LAST]->(m {step:line.PrevStatus})
FOREACH(y IN CASE WHEN m IS NOT NULL THEN [1] ELSE [] END | DELETE delRel MERGE (m)-[:NEXT {time:line.Time}]->(n))

// This is an acception node. We still check for safety's sake. 

WITH line

MATCH (pl:MarginCall {id:line.CallId})
WHERE line.NewStatus = 'Accepted'
SET pl.assetsSettled=pl.assetsPledged, pl.settledQuantities=pl.pledgedQuantities, pl.settledCustodianAccount=pl.pledgedCustodianAccount
REMOVE pl.assetsPledged, pl.pledgedQuantities, pl.pledgedCustodianAccount
WITH line, pl, RANGE(0, SIZE(SPLIT(pl.assetsSettled, '|'))-1) as len
UNWIND(len) as i
	MATCH (a:Asset {id:SPLIT(pl.assetsSettled, '|')[i]})<-[h:HOLDS]-(:CustodianAccount {id:SPLIT(pl.settledCustodianAccount, '|')[i]})
	SET h.pledgedQuantities = h.pledgedQuantities - toFloat(SPLIT(pl.settledQuantities, '|')[i])
	SET h.settledQuantities = h.settledQuantities + toFloat(SPLIT(pl.settledQuantities, '|')[i])

// No change in available quantities since these assets had already been pledged
