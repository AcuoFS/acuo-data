LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/AcuoFS/acuo-data/master/CSV/margincall/info7.csv' 
AS line

MATCH (mc:MarginCall {id:line.CallId})

MERGE (mc)-[:LAST]->(n:CallStep {step:line.NewStatus, time:line.Time})

WITH mc, n, line,
		CASE WHEN NOT ((mc)-[:FIRST]->()) THEN [1] ELSE []
		END
	AS firstNode

FOREACH (i IN firstNode | MERGE (mc)-[:FIRST]->(n))
WITH mc, n, line

// We delete the previous [:LAST] relationship. In case there's a split (partial dispute), we ensure the proper [:LAST] node is deleted (correct PrevStatus, different time). 

MATCH (n)<-[:LAST]-(mc)-[delRel:LAST]-(m {step:line.PrevStatus})
WHERE m.time <> n.time
DELETE delRel

MERGE (m)-[:NEXT]->(n)
