LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/AcuoFS/acuo-data/master/CSV/valuemarkit.csv' 
AS line

WITH *

MATCH (t:Trade {id:line.TradeId})-[:BELONGS_TO]->(p:Portfolio)

MERGE (t)-[:VALUATED]->(v1:Valuation {date:line.Date})

MERGE (v1)-[:VALUE {time:line.creationTime}]->(nv1:Value {source:line.Source, currency:line.Currency})

SET nv1.PV = toFloat(line.PV)

// When the portfolio is implemented : go fetch the portfolio's valuation and increment its Markit PV. 

WITH *

MERGE (p)-[:VALUATED]->(v2:Valuation {date:line.Date})

MERGE (v2)-[vr:VALUE]->(nv2:Value {source:line.Source, currency:line.Currency})
ON CREATE SET vr.time = line.creationTime, vr.lastUpdate=line.creationTime
ON MATCH SET vr.lastUpdate=line.creationTime

WITH *,
		CASE WHEN NOT exists((v2)-[:LEAD_VALUE]->(:Value)) THEN [1] ELSE []
		END
	as noLead

FOREACH (i in noLead | MERGE (v2)-[:LEAD_VALUE]->(nv2))
